#library "huw.acs"
#include "zcommon.acs"

#define MAX_VOLUME 127
#define MAX_PLAYER_HEALTH 200

//fadescript for map setup - from BoA
script "Maplaunch" ENTER
{
	If((GetLevelInfo(LEVELINFO_LEVELNUM)==99)) //prevent fading from following maps
		terminate;
	FadeRange(0,0,0,1.0,0,0,0,0.0,1.5);
}

//lowhealth scripts - from BoA, sounds should be replaced with lowhealth music
script "30Health" ENTER
{
	int width = 640, height = 480;
	SetHudSize(width, height, 0);
	SetFont("M_INJ");

	// Half width and height (in fixed-point).
	int half_width  = (width / 2) << 16;
	int half_height = (height / 2) << 16;

	while (true)
	{
		int health = GetActorProperty(0, APROP_HEALTH);
		if (health > 0 && health <= 30)
		{
			HudMessage(s:"A"; HUDMSG_FADEINOUT, 0, CR_WHITE, half_width, half_height, 0.0, 0.5, 0.5);

			// Volume and delay of heartbeat (faster and louder with less health).
			int volume = MAX_VOLUME;
			int delay1 = 10;
			int delay2 = 14;

			if (health > 20)
			{
				volume = MAX_VOLUME - 50;
				delay1 = 21;
				delay2 = 34;
			}
			else if (health > 10)
			{
				volume = MAX_VOLUME - 25;
				delay1 = 18;
				delay2 = 24;
			}

			LocalAmbientSound("hbeat", volume);
			Delay(delay1);
			LocalAmbientSound("hbeat", volume);
			Delay(delay2);
		}

		Delay(1);
	}
	
}

//death script
script "OhNoes!" DEATH
{
	SetHudSize(320, 240, true);
	LocalSetMusic("UW_Death");

	SetFont("fade");
	HudMessage(s:"A"; HUDMSG_FADEINOUT | HUDMSG_NOWRAP | HUDMSG_LAYER_OVERHUD, -1000, CR_UNTRANSLATED, 160.0, 120.0, 999999.0, 6.0, 1.0);

	// [ZK] If you resurrect, the death screen should disappear immediately
	for (int i = 0; i < 35*6; i++)
	{
		Delay(1);

		if (GetActorProperty(0, APROP_HEALTH) > 0)
		{
			SetFont("fade");
			HudMessage(s:""; HUDMSG_PLAIN, -1000, CR_UNTRANSLATED, 0.0, 0.0, 0.0, 0.0);
			LocalSetMusic("*");
			terminate;
		}
	}

	SetFont("death");
	HudMessage(s:"A"; HUDMSG_FADEINOUT | HUDMSG_NOWRAP | HUDMSG_LAYER_OVERHUD, -1001, CR_UNTRANSLATED, 160.0, 120.0, 999999.0, 3.0, 1.0);

	// [ZK] Delay 1 tic instead of 35
	while(GetActorProperty(0,APROP_HEALTH) <= 0) Delay(1);

	//fadeback if resurrected
	HudMessage(s:"A"; HUDMSG_FADEINOUT | HUDMSG_NOWRAP | HUDMSG_LAYER_OVERHUD, -1001, CR_UNTRANSLATED, 160.0, 120.0, 0.0, 0.0, 0.25);
	SetFont("fade");
	HudMessage(s:"A"; HUDMSG_FADEINOUT | HUDMSG_NOWRAP | HUDMSG_LAYER_OVERHUD, -1000, CR_UNTRANSLATED, 160.0, 120.0, 0.0, 0.0, 1.0);

	// [ZK] restore music
	LocalSetMusic("*");
}